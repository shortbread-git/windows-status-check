Option Explicit
Class NetworkInfo

	Private networkDict_

	Public Sub Class_Initialize()
	
		Call setNetworkStatus()
	
	End Sub

	Private Sub setNetworkStatus()

		
		Dim oLocator
		Dim oService
		Dim networkAdapts
		Dim networkAdapters
		Dim networkConfigs
		Dim adapterDict
		Dim configDict

		Set oLocator        = WScript.CreateObject("WbemScripting.SWbemLocator")
		Set oService        = oLocator.ConnectServer
		Set networkAdapters = oService.ExecQuery("Select * From Win32_NetworkAdapter Where NetConnectionID IS NOT NULL")
		Set networkConfigs  = oService.ExecQuery("Select * From Win32_NetworkAdapterConfiguration")
		
		Set adapterDict = createAdapterDict(networkAdapters)
		Set configDict  = createConfigDict(networkConfigs)
		Set networkDict_ = CreateObject("Scripting.Dictionary")
		
		Call createNetworkDict(adapterDict,configDict)
		
	End Sub
	
	Public Sub display()
	
		WScript.echo "********************************************************************"
		WScript.echo "* Networks                                                         *"
		WScript.echo "********************************************************************"		
		WScript.echo ""
		
		Dim adapterInfo
		Dim count
		Dim networkId
		For Each networkId In networkDict_
			
			Dim conId
			Dim adapterName
			Dim mac
			Dim ip
			Dim subnet
			Dim dhcp
			Dim status
			
			conId       = networkDict_.Item(networkId).Item("conId")
			adapterName = networkDict_.Item(networkId).Item("adapterName")
			mac         = networkDict_.Item(networkId).Item("mac")
			ip          = networkDict_.Item(networkId).Item("ip")
			subnet	    = networkDict_.Item(networkId).Item("subnet")
			dhcp        = networkDict_.Item(networkId).Item("dhcp")
			status      = networkDict_.Item(networkId).Item("status")
			
			WScript.echo "接続名　　 ： " & conId
			WScript.echo "アダプタ名 ： " & adapterName
			WScript.echo "Macアドレス： " & mac
			WScript.echo "IPアドレス ： " & ip
			WScript.echo "SubnetMask ： " & subnet
			WScript.echo "DHCP　　　 ： " & dhcp
			WScript.echo "Status　　 ： " & status
			WScript.echo ""
		
		Next
	End Sub
	
	Private Function createAdapterDict(networkAdapters)

		Dim adapterDict
		Dim oneOfAdapter
		Dim count 
		
		Set adapterDict = CreateObject("Scripting.Dictionary")
		
		count = 0
		For Each oneOfAdapter In networkAdapters

			Dim tmpDict
			Dim mac
			Set tmpDict = CreateObject("Scripting.Dictionary")

			mac = oneOfAdapter.MACAddress
			If IsNull(mac) then
				mac = ""
			End If
		
			tmpDict.Add "conId"       , oneOfAdapter.NetConnectionID
			tmpDict.Add "adapterName" , oneOfAdapter.Name	
			tmpDict.Add "mac"         , mac
			tmpDict.Add "status"      , convertStatusMsg(oneOfAdapter.NetConnectionStatus)
			
			Dim adapterId 
			adapterId = "adapter" & count
			adapterDict.Add adapterId , tmpDict
			
			count = count + 1
		
		Next
		
		Set createAdapterDict = adapterDict
		
	End Function
	
	Private Function createConfigDict(networkConfigs)
		
		Dim configDict
		Dim oneOfConfig
		Dim count
		Set configDict = CreateObject("Scripting.Dictionary")
		
		count = 0
		For Each oneOfConfig In networkConfigs
		
			Dim tmpDict
			Set tmpDict = CreateObject("Scripting.Dictionary")
			
			tmpDict.Add "adapterName" , oneOfConfig.Description
			tmpDict.Add "dhcp"        , CStr(oneOfConfig.DHCPEnabled)
			
			If oneOfConfig.IPEnabled = True Then 
				tmpDict.Add "ip"     , oneOfConfig.IPAddress(0)
				tmpDict.Add "subnet" , oneOfConfig.IPSubnet(0)
			Else
				tmpDict.Add "ip"     , ""
				tmpDict.Add "subnet" , ""
			End If
			
			Dim configId 
			configId = "config" & count
			configDict.Add configId , tmpDict
			
			count = count + 1
		
		Next
		
		Set createConfigDict = configDict
		
	End Function
	
	
	Private Sub createNetworkDict(adapterDict,configDict)
				
		Dim count
		Dim oneOfAdapter
		
		count = 0
		For Each oneOfAdapter In adapterDict
			
			Dim tmpDict
			Dim a_adapterName
			Dim oneOfConfig
			
			a_adapterName     = adapterDict.Item(oneOfAdapter).Item("adapterName")
			Set tmpDict       = CreateObject("Scripting.Dictionary")
			
			For Each oneOfConfig In configDict
				
				Dim c_adapterName
				c_adapterName = configDict.Item(oneOfConfig).Item("adapterName")
									
				If StrComp(a_adapterName , c_adapterName) = 0 Then
					If Not(tmpDict.Exists("conId")) Then
						tmpDict.Add "conId"       , adapterDict.Item(oneOfAdapter).Item("conId")
						tmpDict.Add "adapterName" , a_adapterName
						tmpDict.Add "mac"         , adapterDict.Item(oneOfAdapter).Item("mac")
						tmpDict.Add "ip"          , configDict.Item(oneOfConfig).Item("ip")
						tmpDict.Add "subnet"      , configDict.Item(oneOfConfig).Item("subnet")
						tmpDict.Add "dhcp"        , configDict.Item(oneOfConfig).Item("dhcp")
						tmpDict.Add "status"      , adapterDict.Item(oneOfAdapter).Item("status")
						
						Dim networkId
						networkId = "network" & count
						networkDict_.Add networkId , tmpDict
					End If
				End If
			Next
			
			count = count + 1
			
		Next
	End Sub
	
	Private Function convertStatusMsg(statusNum)
	
		Dim statusMessage
		
		Select Case statusNum
			Case 0
				statusMessage = "リンクダウン"
			Case 1
				statusMessage = "接続中..."
			Case 2
				statusMessage = "リンクアップ"
			Case 3
				statusMessage = "接続中..."
			Case 4
				statusMessage = "ハードウェアがありません"
			Case 5
				statusMessage = "無効"
			Case 6
				statusMessage = "ハードウェアが正常に機能していません"
			Case 7
				statusMessage = "メディアが切断状態です"
			Case 8
				statusMessage = "認証中..."
			Case 9
				statusMessage = "認証に成功しました"
			Case 10
				statusMessage = "認証に失敗しました"
			Case 11
				statusMessage = "アドレスが無効"
			Case 12
				statusMessage = "資格情報が必要"
			Case Else
				statusMessage = "不明なエラー。Win32エラーコードを参照してください"
		End Select
		
		convertStatusMsg = statusmessage
	
	End Function
	
End Class
